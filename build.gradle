import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext {
        compose_ui_version = '1.6.1'
        compose_nav_version = "2.7.7"
        compose_lint_version = '1.3.1'
        compose_activity_version = '1.8.2'
        compose_material_version = '1.6.1'
        lifecycle_version = '2.7.0'
        material_version = '1.11.0'
        core_ktx_version = '1.12.0'
    }
}
// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.1.2' apply false
    id 'com.android.library' version '8.1.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.0' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id "com.autonomousapps.dependency-analysis" version '1.30.0'
}

apply from: "${rootDir}/scripts/publish-root.gradle"

version = rootProject.ext['version']

subprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            if (project.findProperty("composeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
            if (project.findProperty("composeCompilerMetrics") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
        }
    }
}