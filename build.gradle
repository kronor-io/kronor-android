import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext {
        compose_ui_version = '1.9.1'
        compose_nav_version = '2.9.4'
        compose_lint_version = '1.4.2'
        compose_activity_version = '1.11.0'
        compose_material_version = '1.9.1'
        lifecycle_version = '2.9.4'
        material_version = '1.13.0'
        core_ktx_version = '1.17.0'
    }
}
// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.10.1' apply false
    id 'com.android.library' version '8.10.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.0' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id "com.autonomousapps.dependency-analysis" version '1.30.0'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.0.21' apply false
}

apply from: "${rootDir}/scripts/publish-root.gradle"

version = rootProject.ext['version']

subprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            if (project.findProperty("composeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
            if (project.findProperty("composeCompilerMetrics") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
        }
    }
}